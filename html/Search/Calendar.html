<%args>
$Month => (localtime)[4]
$Year => (localtime)[5] + 1900
</%args>
<& /Elements/Header, Title => $title &>

% if ( $m->comp_exists( '/Ticket/Elements/Tabs' ) ) {
<& /Ticket/Elements/Tabs,
    current_tab => "Search/Calendar.html?" . $m->comp( '/Elements/QueryString', %query ),
    Title => $title &>
% } else {
    <& /Elements/Tabs, QueryArgs => \%query &>
% }
<& /Elements/ListActions, actions => \@results &>

<&| /Widgets/TitleBox,
     title => $title,
     title_class=> 'inverse',
     color => "#993333" &>
<& /Search/Elements/Calendar, %ARGS, %query &>
</&>

<div class="rtxcalendar-meta">
<form method="get" action="<%RT->Config->Get('WebPath')%>/Search/Calendar.html">
% foreach my $var ( qw(Query Format OrderBy Order RowsPerPage) ) {
<input type="hidden" class="hidden" name="<%$var%>" value="<%$query{$var} || ''%>" />
%}
<div class="rtxcalendar-properties">
<&| /Widgets/TitleBox, title => loc('Calendar Properties')&>

<&|/l&>Calendar Type:</&>
<& Elements/SelectCalendarType, Name => 'CalendarType', Default => $query{'CalendarType'} &>

<&|/l_unsafe, $m->scomp('Elements/SelectCalendarDateType',
                        Name => 'TimeSpanOpen',
                        Default => $query{'TimeSpanOpen'}),
              $m->scomp('Elements/SelectCalendarDateType',
                        Name => 'TimeSpanClose',
                        Default => $query{'TimeSpanClose'}) 
&>Always display tickets between [_1] and [_2]</&>

<input type="submit" class="button" value="<%loc('Update Calendar')%>" />
</&>
</div>

<div class="saved-search">
<input type="hidden" class="hidden" name="SavedSearchId" value="<% $saved_search->{Id} || 'new' %>" />
<& /Search/Elements/EditSearches,
    %$saved_search,
    Title         => loc('Saved calendars'),
    SearchFields  => \@save_arguments,
    CurrentSearch => \%query,
    AllowCopy     => 1,
&>
</div>

</form>
</div>
<%init>

# Default Query and Format
if ( my $Search = RTx::Calendar::SearchDefaultCalendar($session{CurrentUser}) ) {
    $ARGS{'Format'} ||= $Search->SubValue('Format');
    $ARGS{'Query'}  ||= $Search->SubValue('Query');
    $ARGS{'OrderBy'} ||= $Search->SubValue('OrderBy');
    $ARGS{'Order'}   ||= $Search->SubValue('Order');
    $ARGS{'RowsPerPage'}  ||= $Search->SubValue('RowsPerPage');
}
$ARGS{'Format'} ||= "__Starts__ __Due__";
$ARGS{'Query'} ||= "( Status = 'new' OR Status = 'open' OR Status = 'stalled')
 AND ( Owner = '" . $session{CurrentUser}->Id ."' OR Owner = 'Nobody'  )
 AND ( Type = 'reminder' OR 'Type' = 'ticket' )";
$ARGS{'OrderBy'} ||= 'id';
$ARGS{'Order'} ||= 'ASC';
$ARGS{'RowsPerPage'} ||= '50';
$ARGS{'CalendarType'} ||= 'month';

my @results;
my @save_arguments = qw(Query Format OrderBy Order RowsPerPage CalendarType TimeSpanOpen TimeSpanClose);
my $saved_search = { Type => 'Calendar' };
my %query;
for( @save_arguments ) {
    $query{$_} = $ARGS{$_};
}
unless ( $query{'TimeSpanOpen'} && $query{'TimeSpanClose'} ) {
    delete $query{'TimeSpanOpen'};
    delete $query{'TimeSpanClose'};
}

push @results, $m->comp( '/Search/Elements/EditSearches:Init',
    %ARGS,
    Query        => \%query,
    SavedSearch  => $saved_search,
    SearchFields => \@save_arguments,
);

my $description = '';
$description = $saved_search->{'Description'} if $saved_search->{'Description'};
$ARGS{'SavedSearchId'} = $saved_search->{'Id'} if $saved_search->{'Id'};
if ( $saved_search->{'Object'} ) {
    for( @save_arguments ) {
        $query{$_} ||= $ARGS{$_} ||= $saved_search->{'Object'}->SubValue($_) if $saved_search->{'Object'}->SubValue($_);
    }
}

push @results, $m->comp( '/Search/Elements/EditSearches:Save',
    %ARGS,
    Query        => \%ARGS,
    SavedSearch  => $saved_search,
    SearchFields => \@save_arguments,
);

my $title = loc( "Calendar [_1]", $description );

</%init>
